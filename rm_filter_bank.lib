// vim:ft=faust
declare name        "Regalia-Mitra Filter Bank";
declare version     "0.1";
declare author      "Marc Joliet";
declare license     "MIT";
declare copyright   "(c)Marc Joliet 2013";

import("filter.lib");
import("math.lib");
import("poly.lib");
import("filter_helpers.lib");

// Generate the coefficients Q(z) of the filter Q(z)/D(z) that is
// double-complementary to the filter P(z)/D(z).
gpqN(P,D,N) = Q with {
    P_sq = P:polysqN(N);
    D_sq = (D,(D:reverseN(N))):poly_multN(N,N);
    M = 2*N-1;
    r(i) = -(P_sq:selector(i,M), D_sq:selector(i,M));
    q(0) = sqrt(r(0));
    q(1) = r(1)/(2*q(0));
    q(i) = (r(i) - sum(k,i-1,(q(k+1)*q(i-k-1))))/(2*q(0));
    Q = par(i,N,q(i));
};

gpq(P,D) = Q with {
    N = count(P);
    P_sq = polysq(P);
    D_sq = poly_mult(D,reverse(D));
    M = 2*N-1;
    r(i) = take(i+1,P_sq) - take(i+1,D_sq);
    q(0) = sqrt(r(0));
    q(1) = r(1)/(2*q(0));
    q(i) = (r(i) - sum(k,i-1,(q(k+1)*q(i-k-1))))/(2*q(0));
    Q = par(i,N,q(i));
};

// Returns a 3rd order low-pass filter with a Cauer design (the same as
// lowpass3e) along with its corresponding double-*c*omplementary high-pass.
bandsplit3ec(fc) = lp,hp
with {
    N = 3;
    M = N-1;

    // analog coefficients ("[z,p,g] = ncauer (0.2, 60, 3)" in octave)
    a11 = 0.802636764161030; // format long; poly(p(1:2)) % in octave
    a01 = 1.412270893774204;
    a02 = 0.822445908998816; // poly(p(3)) % in octave
    b21 = 0.019809144837789; // g*poly(z)
    b11 = 0;
    b01 = 1.161516418982696;
    w1 = 2*PI*fc;

    // generate the low-pass
    lp = tf2s(b21,b11,b01,a11,a01,w1) : tf1s(0,1,a02,w1);

    // Generate the double-complementary high-pass from the low-pass coefficients.
    A = tf2s_coeffs(b21,b11,b01,a11,a01,w1);
    B = tf1s_coeffs(0,1,a02,w1);
    P = poly_mult(getbcoeffs(B),getbcoeffs(A));
    D = poly_mult(getacoeffs(B),getacoeffs(A));
    Q = gpq(P,D);
    hp = iir(Q,(rest(D)));
};

// Creates a Regalia-Mitra style double-complementary analysis filter bank.
//
// Sadly, this does not use the all-pass structure as proposed by Regalia and
// Mitra. Instead, it implements the equivalent structure using the
// double-complementary low-pass and high-pass pairs.
//
// NOTE: After processing, you might want to use the synthesis filter bank
// below, since the complete transfer function becomes invariant to changes in
// the filters (i.e., changing an edge frequency), according to Regalia-Mitra.
rm_filterbank_analyse3e(fe) = fb with {
    N = count(fe);
    w(i) = take(i+1,fe);
    stage(0) = _ <: bandsplit3ec(w(N-1));
    next(i)  = par(j,i+1,_<:_,_):par(j,i+1,bandsplit3ec(w(N-1-i))):bus(2),par(j,i,+);
    stage(i) = stage(i-1):next(i);
    fb = stage(N-1);
};

// Creates a Regalia-Mitra style double-complementary synthesis filter bank.
// See the analysis filter bank above for comments.
rm_filterbank_synthesize3e(fe) = fb with {
    N = count(fe);
    w(i) = take(i+1,fe);
    stage(0) = _,_:bandsplit3ec(w(N-1)):-;
    prev(i)  = (_,_:bandsplit3ec(w(N-1-i)):-),par(j,i,_<:bandsplit3ec(w(N-1-i)):-);
    stage(i) = prev(i):stage(i-1);
    fb = stage(N-1);
};

// some demo functions
rm_filterbank_analyse_demo =
rm_filterbank_analyse((1000,2000,3000,4000,5000,6000,7000,8000));
rm_filterbank_synthesize_demo =
rm_filterbank_analyse((1000,2000,3000)):rm_filterbank_synthesize((1000,2000,3000));
